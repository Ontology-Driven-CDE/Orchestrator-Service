PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX fpo: <http://w3id.org/fpo#>
PREFIX fso: <http://w3id.org/fso#>
insert {
  ?component fso:feedsFluidTo ?fictivCapComponentID . 
  ?fictivCapComponentID a fso:Cap .} 
where {
  VALUES ?componentType {fso:Duct fso:Pipe}
  ?component a ?componentType .
  filter not exists {?component fso:feedsFluidTo ?anotherComponent} .
  BIND(IRI(CONCAT("https://example.com/inst#",STRUUID())) as ?fictivCapComponentID) .
};
insert {
   ?fictivCapComponentID fso:feedsFluidTo ?component .
   ?fictivCapComponentID a fso:Cap .
}
where {
  VALUES ?componentType {fso:Duct fso:Pipe}
  ?component a ?componentType .
  filter not exists {?anotherComponent fso:feedsFluidTo  ?component}
  
  BIND(IRI(CONCAT("https://example.com/inst#",STRUUID())) as ?fictivCapComponentID)
  }
