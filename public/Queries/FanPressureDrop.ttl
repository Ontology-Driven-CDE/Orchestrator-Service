PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <https://example.com/ex#>
PREFIX fo: <http://purl.org/ontology/fo/>
PREFIX bot: <https://w3id.org/bot#>
PREFIX fpo: <http://w3id.org/fpo#>
PREFIX fso: <http://w3id.org/fso#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX inst: <https://example.com/inst#> 
select ?fan (Max(?sumOfPressureDrop)*0.00010199773339984 as ?head)
where{
{
  select ?fan ?terminal ( sum(?supplyValue)  as ?sumOfPressureDrop) 
where {
  ?fan a fpo:Fan .
  VALUES ?terminalType {fpo:AirTerminal}
  ?terminal a ?terminalType .  
  ?supplySystem fso:hasComponent ?fan .
  ?supplyComponent fso:feedsFluidTo+ ?terminal . 
  ?supplySystem fso:hasComponent ?supplyComponent .
  ?supplySystem a fso:SupplySystem .
  
  #Get the pressuredrop value of each component
  ?supplyComponent fso:hasPort ?supplyPort .
  ?supplyPort fpo:flowDirection ?flowDirection .
  ?flowDirection fpo:value "Out" .
  ?supplyPort fpo:pressureDrop ex:UUIDPressureDropValue .  
  ex:UUIDPressureDropValue fpo:value ?supplyValue .
  
  #Select only port that is part of that system
  filter not exists {
  ?supplyPort fso:suppliesFluidTo ?connectedPort .
  ?connectedComponent fso:hasPort ?connectedPort .
  ?connectedComponent fso:feedsFluidTo+ ?terminal .
  ?connectedComponent a fpo:Tee .
  }
    } group by ?fan ?terminal}
  union
  {
    select ?fan ?terminal( sum(?returnValue)  as ?sumOfPressureDrop) 
where {
  ?fan a fpo:Fan .
  VALUES ?terminalType {fpo:AirTerminal}
  ?terminal a ?terminalType .  
  ?supplySystem fso:hasComponent ?fan .
  ?returnComponent ^fso:feedsFluidTo+ ?terminal . 
  ?returnSystem fso:hasComponent ?returnComponent .
  ?returnSystem a fso:ReturnSystem .
  
  #Get the pressuredrop value of each component
  ?returnComponent fso:hasPort ?returnPort .
  ?returnPort fpo:flowDirection ?flowDirection .
  ?flowDirection fpo:value "Out" .
  ?returnPort fpo:pressureDrop ex:UUIDPressureDropValue .  
  ex:UUIDPressureDropValue fpo:value ?returnValue .
  } group by ?fan ?terminal
} 
} group by ?fan 