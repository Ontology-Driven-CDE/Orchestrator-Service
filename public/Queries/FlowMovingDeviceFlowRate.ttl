PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <https://example.com/ex#>
PREFIX fo: <http://purl.org/ontology/fo/>
PREFIX bot: <https://w3id.org/bot#>
PREFIX fpo: <http://w3id.org/fpo#>
PREFIX fso: <http://w3id.org/fso#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX inst: <https://example.com/inst#> 
SELECT ?flowMovingDeviceType ?flowMovingDevice (SUM(?flowRateValue) AS ?totalFlow)
    WHERE{
  {
  ?flowMovingDevice a fpo:Fan .
  ?flowMovingDevice a ?flowMovingDeviceType .
  ?system fso:hasComponent ?flowMovingDevice .
  {?flowMovingDevice ^fso:feedsFluidTo+ ?terminal .}
  union
  { ?flowMovingDevice fso:feedsFluidTo+ ?terminal .}
  	?terminal a fpo:AirTerminal .
    ?terminal fso:hasPort ?returnPort .
  	?returnPort fpo:flowRate ?flowRate .  
  	?flowRate fpo:value ?flowRateValue .
    }
  union
  {
    ?flowMovingDevice a fpo:Pump .
    ?flowMovingDevice a ?flowMovingDeviceType .
    ?flowMovingDevice fso:feedsFluidTo+ ?terminal .
    VALUES ?type {fpo:HeatExchanger fpo:SpaceHeater} ?terminal a ?type .
    ?terminal fso:hasPort ?supplyPort .
  	?supplyPort fso:returnsFluidTo ?anotherComponent .
  	?supplyPort fpo:flowRate ?flowRate .  
  	?flowRate fpo:value ?flowRateValue .
  }
    } group by ?flowMovingDevice ?flowMovingDeviceType
   